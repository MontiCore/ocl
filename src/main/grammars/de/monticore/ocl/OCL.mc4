// (c) https://github.com/MontiCore/monticore
package de.monticore.ocl;

/* This is a MontiCore stable grammar.
 * Adaptations -- if any -- are conservative. */

import de.monticore.UMLStereotype;
import de.monticore.ocl.expressions.*;
import de.monticore.expressions.*;
import de.monticore.types.*;
import de.monticore.literals.*;

grammar OCL extends UMLStereotype,
                    MCSimpleGenericTypes,
                    CommonExpressions,
                    SetExpressions,
                    OCLExpressions,
                    OptionalOperators,
                    BitExpressions,
                    UglyExpressions
{
  /**
   *  ASTOCLCompilationUnit represents the entire content of an OCL model
   *    @attribute package
   *               The package declaration of the component model
   *    @attribute MCImportStatement
   *               List of imported elements
   *    @attribute OCLArtifact
   *               wrapper for the ocl constraints
   */
  OCLCompilationUnit =
    ("package" package:(Name& || ".")+ ";")?
    MCImportStatement*
    OCLArtifact
  ;

  /**
   * ASTOCLArtifact represents a file that contains OCL-constraints.
   *   @attribute Name
   *              Name of OCLFile (necessary for MCCompilationUnit)
   *   @attribute OCLConstraint
   *              List of OCL-constraints.
   */
  OCLArtifact implements Diagram =
    "ocl" Name "{"
      OCLConstraint*
    "}"
  ;

  /**
   * ASTOCLParamDeclaration defines a parameter for a method
   *   @attribute MCType
   *              Type of the parameter
   *   @attribute Name
   *              Name of the parameter
   *   @attribute Expression
   *              default value of the parameter
   */
  OCLParamDeclaration implements Variable =
    MCType Name ("=" Expression)?;

  /**
   * ASTOCLConstraint subsumes all OCL types that are used as embedded OCL.
   *   An OCLConstraint (Invariant or Method- or
   *   ConstructorSpec) or an abstract expression (expression).
   */
  interface OCLConstraint;

  /**
   * ASTOCLOperationSignature subsumes method and constructor signatures.
   */
  interface OCLOperationSignature;

  /**
   * ASTOCLOperationConstraint represents the typical method or constructor
   *   specifications with pre- and post-conditions.
   *   @attribute Stereotype
   *              Optional stereotypes
   *   @attribute OCLOperationSignature
   *              Operation Signature (of a method or constructor).
   *   @attribute OCLVariableDeclaration
   *              Optional variable declarations.
   *   @attribute OCLPreStatement
   *              Optional pre-statements.
   *   @attribute OCLPostStatement
   *              Optional post-statements.
   */
  scope OCLOperationConstraint implements OCLConstraint =
    Stereotype* 
    "context"
    OCLOperationSignature
    ("let"      (OCLVariableDeclaration || ";")+ )?
    ("pre"  ":" ( preCondition:Expression ";")+ )?
    ("post" ":" (postCondition:Expression ";")+ )?
  ;

  /**
   * ASTOCLInvariant represents the typical invariant definitions of OCL.
   *   @attribute Stereotype
   *              Optional stereotypes
   *   @attribute OCLContextDefinition
   *              Optional context in which to evaluate the invariant
   *   @attribute name
   *              Name of the invariant.
   *   @attribute OCLParamDeclaration
   *              parameters of the invariant
   *   @attribute statements
   *              List of boolean expressions.
   */
  symbol scope OCLInvariant implements OCLConstraint =
    Stereotype*
    // class context
    (
      (["context"] | ["import"])
      (OCLContextDefinition || ",")+
    )?

    // invariant
    "inv" Name?
    ("(" (OCLParamDeclaration || ",")* ")")? ":"

    // expression: we can chain multiple expressions using &&
    Expression ";"
  ;

  /**
   * ASTOCLContextDefinition can be of the following type.
   *   Auction | Auction a | a in (...) | Auction a in (...)
   *   The rule is written as follows to avoid matching empty strings
   */
  OCLContextDefinition =
      MCType | GeneratorDeclaration | OCLParamDeclaration
  ;

  /**
   * ASTOCLMethodSignature defines the signature of a Java5 method.
   *   @attribute MCReturnType
   *              Optional complex type that is returned by given method
   *   @attribute methodName
   *              Name of given method.
   *   @attribute OCLParamDeclaration
   *              Parameters of the method.
   *   @attribute throwables
   *              Possible exceptions of the method
   *
   *   Example : context Person.addMessages(Message m):
   */
  OCLMethodSignature implements OCLOperationSignature =
    MCReturnType?
    methodName:MCQualifiedName
    "(" (OCLParamDeclaration || ",")* ")"
    ("throws" throwables:(MCQualifiedName || ",")+)?
  ;

  /**
   * ASTOCLConstructorSignature defines signature of a constructor.
   *   @attribute name
   *              Type of the Constructor.
   *   @attribute OCLParamDeclaration
   *              Parameters of the method.
   *   @attribute throwables
   *              Possible exceptions of the method
   */
  OCLConstructorSignature implements OCLOperationSignature =
    "new" Name
    "(" (OCLParamDeclaration || ",")* ")"
    ("throws" throwables:(MCQualifiedName || ",")+)?
  ;

}
